!***************************************************************************************************************************************
!<license>
!    Copyright (C) 2017, 2018 State of California, Department of Water Resources. This file is part of the CalSim 3.

!    The CalSim 3 is free software: !<license>

!    you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free
!    Software Foundation, either version 3 of the License, or(at your option) any later version.

!    CalSim 3 is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty
!    of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.

!    You should have received a copy of the GNU General Public License along with CalSim 3.  If not, see <http://www.gnu.org/licenses>.
!</license>
!****************************************************************************************************************************************

! NewMelonesForecast.wresl
! Edward Chang
! 4/04/00
! This code calculates two separate New Melones Forecasts
! Revised by C. Hsu, 2/26/03, USBR, for review and documentation project
!######################################################################################

/*
The New Melones forecasts are used to determine demands and minimum flows on the Stanislaus River.
The first forecast (NMforecast1) is the February end of month storage + March thru September
inflows.  This forecast is updated each March.  NMforecast1 is used to determine the minimum fish
and wildlife flows, the Stockton East demands, and the Central San Joaquin demands.  The second
forecast (NMforecast2) is the sum of October thru September inflows.  This forecast is updated
each October.  NMforecast2 is used to determine the Oakdale Irrigation District and the Southern
San Joaquin Irrigation District.
*/
! Define New Melones inflows ( sum of March through September inflows)
define sumI10_part {
    case start{
        condition   month<=FEB .and. wateryear==beginWaterYear
    value 1000.}
    case marthrusep {
        condition   month >= mar .and. month <= sep
        sum(i=mar-month,mar-month+6,1) I10(i) * cfs_TAF(i) }
    case otherwise {
        condition   always
        sum(i=-month-mar,-month,1) I10(i) * cfs_TAF(i) }
        }
! New Melones forecast 1, is the February end of month storage plus March through September inflows
define NMforecast1 {
     case FebruarybeginWaterYear {
          condition wateryear == beginWaterYear .and. month <= feb
          value        1700. + /*  S10(-12) + */ sumI10_part }
     case February {
          condition month == feb
          value          S10(-12) + sumI10_part }
    case marchtofeb {
        condition   always
        value       S10(prevfeb) + sumI10_part }
        }
! New Melones forecast 2, is the sum of October thru September inflows
define NMforecast2 {
     case October {
          condition month == oct
          sum(i=0,11,1) I10(i) * cfs_TAF(i) }
     case otherwise {
          condition always
          sum(i=prevoct,sep-month,1) I10(i) * cfs_TAF(i) }
          }

/*
Define annual delivery quantity using look up table.  Starts at march and forecasts through feb
to be used with stanislaus minflows in cycle 1 and 3
*/
!define stanfish_yr { select stanf from stan_yr given NMF = NMforecast1 use linear }
! Alias stanfih_yr for output
!define stanfish_yrDV {alias stanfish_yr units 'taf'}

define sumI10p_a     {alias sumI10_part units 'taf'}
define NMforecast1dv {alias NMforecast1 kind 'forecast' units 'taf'}
define NMforecast1_a {alias NMforecast1 kind 'forecast' units 'taf'}
define NMforecast2_a {alias NMforecast2 kind 'forecast' units 'taf'}
