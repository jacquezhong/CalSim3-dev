!***************************************************************************************************************************************
!<license>
!    Copyright (C) 2017, 2018 State of California, Department of Water Resources. This file is part of the CalSim 3.

!    The CalSim 3 is free software: !<license>

!    you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free
!    Software Foundation, either version 3 of the License, or(at your option) any later version.

!    CalSim 3 is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty
!    of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.

!    You should have received a copy of the GNU General Public License along with CalSim 3.  If not, see <http://www.gnu.org/licenses>.
!</license>
!****************************************************************************************************************************************

! MokWYT.wresl

/*
Define two water year types:
  1) For Joint Settlement Agreement that governs flow requirements on the lower Mokelumne River below Camanche Reservoir
  2) FERC requirements for PG&E Project 137 on the North Fork Mokelumne River
*/

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!	
!!!!!!!!!!!!!!!! Joint Settlement Agreement !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

/*
Define the wateryear type on which the flow standard are based October through March. Water year types are determined 
by combined Pardee and Camanche storage on November 5th April through September, water year types are determined by
water year unimpaired runoff into Pardee Reservoir as forecasted by DWR in the April 1st Bulletin 120 Report, except
when the forecasted Pardee/Camanche storage on November 5th is projected to be less than 200 TAF.
*/

! Define unimpaired mokelumne river inflow time series
define MOK079FNF      {timeseries kind 'Unimpaired-Flow' units 'TAF'}
define annMOK079FNFdv {std kind 'Unimpaired-Flow' units 'TAF'}

! Calculate annual inflow based on runoff to date plus forecasted (perfect foresight)
define annMOK079FNF {
    case FirstYear {
        condition wateryear == BgnWY .and. month <= JAN
        value 731 }! Based on average of 1922-2009 period
    case Feb {
        condition   month == FEB
        sum(i=1-month,Sep-month,1) MOK079FNF(i) }
    case otherwise {
        condition always
        value annMOK079FNFdv(-1)}
}
goal setannMOK079FNF {annMOK079FNFdv = annMOK079FNF}

define wyt_MokJSAdv {std kind 'WATER-YEAR-TYPE' units 'NONE'}
define wyt_MokJSA {
    case OctMar_critical {
        condition month<APR .and. S_Parde(prevOct)+S_Cmche(prevOct)<270.
        value 4}
    case OctMar_dry {
        condition month<APR .and. S_Parde(prevOct)+S_Cmche(prevOct)<400.
        value 3}
    case OctMar_blwnor {
        condition month<APR .and. S_Parde(prevOct)+S_Cmche(prevOct)<448. - 0.1 !Capacity (210+438) less 200 TAF flood control on November 5th, less buffer
        value 2}
    case OctMar_norabvnor {
        condition month<APR 
        value 1}
    case otherwise {
        condition always
        select yeartype from Mok_wytJSA given inflow=annMOK079FNF use minimum}
}

goal setwyt_MokJSAdv {wyt_MokJSAdv = wyt_MokJSA} 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!	
!!!!!!!!!!!!!!!! North Fork Mokelumne !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

/*
Water Year Types.
The minimum streamflow schedules have been separated into five water year types:
Wet, Above Normal (AN), Below Normal (BN), Dry, and Critically Dry (CD).
The Licensee shall determine water year type based on the predicted unimpaired inflow to
Pardee Reservoir (Pardee) and spring forecasting information provided by the Licensee and
the California Department of Water Resources snowpack forecasts each month from February
through May. The water year types are defined as ollows:

Wet: 	Pardee Inflow >= 958,700 AF
AN: 	Pardee Inflow < 958,700 AF but >= 724,400 AF
BN: 	Pardee Inflow < 724,400 AF but >= 518,100 AF
Dry: 	Pardee Inflow < 518,100 AF but >= 376,100 AF
CD:	Pardee Inflow < 376,100 AF

Each February through May the Licensee shall make a monthly forecast of the water year
type and operate for that month based on that forecast. The May forecast shall be used to
establish the final water year tyPe for the remaining months of the year until the next
February, when forecasting shall begin again. Salt Springs Reservoir is expected to spill
in Wet, AN, and BN years.
*/


define wyt_NForkMokdv {std kind 'WATER-YEAR-TYPE' units 'NONE'}
define wyt_NForkMok {
    case FirstYear{
    	condition wateryear == BgnWY .and. month <= JAN
    	value 2} ! Average for 1922-2009, not important as water year type only affects flows Feb-Sep
    case Feb_Crit{
        condition month == FEB .and. annMOK079FNF < 376.1
        value 5} 
    case Feb_Dry {
        condition month == FEB .and. annMOK079FNF < 518.1
        value 4}
    case Feb_blwnor {
        condition month == FEB .and. annMOK079FNF < 724.4
        value 3}
    case Feb_abvnor {
        condition month == FEB .and. annMOK079FNF < 958.7
        value 2}
    case Feb_wet {
        condition month == FEB
        value 1}
    case otherwise {
        condition always
        value wyt_NForkMokdv(-1)}
}
goal setwyt_NForkMokdv {wyt_NForkMokdv = wyt_NForkMok} 

