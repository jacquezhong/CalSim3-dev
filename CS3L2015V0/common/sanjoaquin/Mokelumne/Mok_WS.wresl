!***************************************************************************************************************************************
!<license>
!    Copyright (C) 2017, 2018 State of California, Department of Water Resources. This file is part of the CalSim 3.

!    The CalSim 3 is free software: !<license>

!    you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free
!    Software Foundation, either version 3 of the License, or(at your option) any later version.

!    CalSim 3 is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty
!    of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.

!    You should have received a copy of the GNU General Public License along with CalSim 3.  If not, see <http://www.gnu.org/licenses>.
!</license>
!****************************************************************************************************************************************

! MokelumneWS.wresl

/*
define prevdistJVID {
    case October {
        condition month == OCT
        select JVID from Mok_demands where month=12}
    case otherwise {
        condition always
        select JVID from Mok_demands where month=month-1}
        }
*/


!******************************************************************************
! Amador and Calaveras County diversions from above Pardee Reservoir
!******************************************************************************

define AnnualAMADR {value 16.15}
define AnnualCPUD  {value  3.7} !From EBMUDSIM. Diverted under Application A5648F, Permit 16338. Max diversion 2,130 acre-feet. max diversion rate 15 cfs.
define AnnualCCWD  {value  4.4} !From EBMUDSIM.

define distAMADR {select AmadorCo from Mok_demands where month=month}
define distCPUD  {select CPUD from Mok_demands where month=month}
define distCCWD  {select CCWD from Mok_demands where month=month}

define demand_Amador    {value AnnualAMADR * distAMADR * taf_cfs}
define demand_CPUD 	{value AnnualCPUD  * distCPUD  * taf_cfs}
define demand_CCWD 	{value AnnualCCWD  * distCCWD  * taf_cfs}

goal set_D_NFM010_AMADR {D_NFM010_AMADR < ( 1.15/16.15)*demand_Amador}! based on water right
goal set_D_TBAUD_AMADR  {D_TBAUD_AMADR  < (15.00/16.15)*demand_Amador}! based on water right
goal set_D_SFM006_MHILL {D_SFM006_MHILL < demand_CPUD}
goal set_D_MFM010_WSPNT {D_MFM010_WSPNT < demand_CCWD}

!******************************************************************************
! JVID diversions from above Pardee Reservoir
!******************************************************************************
/*
Diversions to Jackson Valley ID (JVID) are made directly from Pardee Reservoir. The gravity diversion requires
a water surface elvation in Pardee of 550 feet. Water not delivered in one month will be delivered in the next month.

JVID has rights to divert up to 3,850 AF of Mokelumne River at a diversion rate of 50 cfs;
the diversion is authorized year-round for domestic and stock watering uses, and between
March and October for irrigation uses (SWRCB Decision 1490). However, JVID does not hold rights to store this
water. JVID’s diversion rights are appropriative, based on a 1927 application, and are
subject to reversion to upstream needs. Reversion to upstream needs is determined by
SWRCB, and any reversion of more than 2,200 AF must be accompanied by a substitute
water source. AWA applied for reversion of 1,050 AF, and is considering substitution of
recycled water for a portion of JVID's Mokelumne River water right. AWA proposes to
discharge tertiary treated effluent in Jackson Creek during winter months. JVID is
evaluating its water rights through a study funded by the County.
Mokelumne River water flows by gravity from Pardee Reservoir to Lake Amador. The
District requests and usually receives 3,850 AF annually from EBMUD, although it is subject
to availability.

The water right permit (A005648B is for 50 cfs maximum diversion, year-round). The annual volume is 5.00 TAF, less
the 1.05 reversion.
*/

define AnnualJVID  {value  3.85}
define distJVID    {select JVID from Mok_demands where month=month}
define demand_JVID {value AnnualJVID * distJVID}

define prevdistJVID {
    case October {
        condition month == OCT
        select JVID from Mok_demands where month=12}
    case otherwise {
        condition always
        select JVID from Mok_demands where month=month-1}
        }
define prevdemand_JVID {value AnnualJVID * prevdistJVID}

define AnnJVIDShortDV {std kind 'SHORTAGE' units 'TAF'}
define AnnJVIDShort {
    case First {
        condition month == OCT .and. wateryear == bgnWY
        value 0.0}
    case March {
        condition month == MAR
        value 0.0}
    case otherwise {
        condition always
        value AnnJVIDShortDV(-1) + prevdemand_JVID - D_PARDE_AMADR(-1)* cfs_taf(-1)}
        }
        
define prevdemand_JVIDdv {alias prevdemand_JVID kind 'temp' units 'taf'}
define demand_JVIDdv {alias demand_JVID kind 'temp' units 'taf'}

goal setAnnJVIDShortDV {AnnJVIDShortDV = AnnJVIDShort}

define PardeJVIDFlag {
    case HiStorage {
        condition S_PARDE(-1)> 161. !use BOM storage as threshold, should be average storage
        value 1.0}
    case otherwise {
        condition always
        value 0.0}
        }
        
goal limitJVID     {D_PARDE_AMADR < demand_JVID*taf_cfs*PardeJVIDFlag + AnnJVIDShort*taf_cfs*PardeJVIDFlag}

!******************************************************************************
! Riparian/Appropriative 
!******************************************************************************
/*
Riparian landowners, who have rights that are tied to the river’s natural flow, and other individuals
and agencies with appropriative rights that predate EBMUD’s rights have claims on Mokelumne
River water that are senior to EBMUD’s rights. These individuals and agencies include ranches,
vineyards, and farms that pump water from the Mokelumne River. Downstream of Camanche Dam,
pumped diversions by riparian and senior appropriative users (not including those described
above) reached about 34,000 AFY in 1972 but since have declined to less than 15,000 acre-feet.

For modeling purposes EBMUD assumes riparian diversions are 20.6 TAF/year in wet years, when the unimpaired
inflow to Pardee is above 250 TAF/year, and 16.1 TAF/year in dry years when the unimpaired inflow to Pardee
is above 250 TAF/year.
*/
define AnnAlloc60n_NA5DV {std kind 'AnnualAlloc' units 'taf'}
define AnnAlloc60n_NA5 {
    case octBgnWY {  ! special case for first wateryear, Sum Oct-Sep
        condition month == OCT .and. wateryear == bgnWY
        value 20.6}
    case Above250 {
        condition month == APR .and. annMOK079FNF>=250.
        value 20.6}
    case Below250 {
        condition month == APR
        value 16.1}
    case otherwise {
        condition always
        value AnnAlloc60n_NA5DV(-1)}
}        
goal setAnnAlloc60n_NA5DV {AnnAlloc60n_NA5DV = AnnAlloc60n_NA5}

define AWRann_60N_NA5dv {std kind 'ANNUAL-APPLIED-WATER' units 'taf'}
define AWRann_60N_NA5{   
       case octBgnWY {  ! special case for first wateryear, Sum Oct-Sep
            condition month == OCT .and. wateryear == bgnWY
                sum(i=0,11,1)  AWR_60N_NA5(i)*cfs_taf(i)}
       case MarEndWY {  ! special case for last wateryear, Sum Oct-Sep
            condition month == MAR .and. wateryear == EndWY
                sum(i=prevOCT,SEP-month,1) AWR_60N_NA5(i)*cfs_taf(i)}
       case March { ! March for wateryears BgnWY - year before EndWY
            condition month == MAR .and. wateryear < EndWY
                sum(i=0,11,1)  AWR_60N_NA5(i)*cfs_taf(i)}
       case Rest { 
            condition always
                value AWRann_60N_NA5dv(-1) } }
goal setAWRann_60N_NA5dv {AWRann_60N_NA5dv = AWRann_60N_NA5}

define AWOann_60N_NA5dv {std kind 'ANNUAL-APPLIED-WATER' units 'taf'}
define AWOann_60N_NA5{   
       case octBgnWY {  ! special case for first wateryear, Sum Oct-Sep
            condition month == OCT .and. wateryear == bgnWY
                sum(i=0,11,1)  AWO_60N_NA5(i)*cfs_taf(i)}
       case MarEndWY {  ! special case for last wateryear, Sum Oct-Sep
            condition month == MAR .and. wateryear == EndWY
                sum(i=prevOCT,SEP-month,1) AWO_60N_NA5(i)*cfs_taf(i)}
       case March { ! March for wateryears BgnWY - year before EndWY
            condition month == MAR .and. wateryear < EndWY
                sum(i=0,11,1)  AWO_60N_NA5(i)*cfs_taf(i)}
       case Rest { 
            condition always
                value AWOann_60N_NA5dv(-1) } }
goal setAWOann_60N_NA5dv {AWOann_60N_NA5dv = AWOann_60N_NA5}

define dist_60N_NA5 {value (AWR_60N_NA5+AWO_60N_NA5)*cfs_taf/(AWRann_60N_NA5+AWOann_60N_NA5) }
define dist60N_NA5_ {alias dist_60N_NA5 kind 'debug' units 'none'}
! distribution per EBMUD urban water management plan
goal set_D_MOK050_60N_NA5 {D_MOK050_60N_NA5 < 0.65 * AnnAlloc60n_NA5 * dist_60N_NA5 * taf_cfs}
goal set_D_MOK039_60N_NA5 {D_MOK039_60N_NA5 < 0.05 * AnnAlloc60n_NA5 * dist_60N_NA5 * taf_cfs}
goal set_D_MOK033_60N_NA5 {D_MOK033_60N_NA5 < 0.30 * AnnAlloc60n_NA5 * dist_60N_NA5 * taf_cfs}


!******************************************************************************
! Woodbridge ID - already reduced for sale to City of Lodi (6 taf in wet
! years and 3 taf in dry years)
!******************************************************************************

/*
WID serves irrigation water to about 20,000 acres of agricultural land in San Joaquin County in the
vicinity of Lodi and Woodbridge. The lower Mokelumne River flows through Lodi Lake, which is an
impoundment created by Woodbridge Dam near Lodi. From this lake, WID diverts water into an
extensive canal system. WID has pre-1914 water rights senior to EBMUD’s Mokelumne River water
rights, and also has two overlapping licensed water rights for direct diversions up to 414.4 cfs, the
maximum capacity of WID Canal. These entitlements are conditioned by the water rights settlement
agreements with EBMUD, which, depending on inflow to Pardee Reservoir, provide WID with firm
annual diversions of 60 TAF in normal water years (inflow >375 TAF) and 39 TAF in dry years.

In normal years, WID provides 6 taf to City of Lodi and 6.5 taf to City of Stockton
In dry years, those values are 3 taf and 3.25 taf to Lodi and Stockton respectively
*/


define AnnAlloc60n_NA4DV {std kind 'AnnualAlloc' units 'taf'}
define AnnAlloc60n_NA4 {
    case octBgnWY {  ! special case for first wateryear, Sum Oct-Sep
        condition month == OCT .and. wateryear == bgnWY
        value 60. - 6. - 6.5}
    case Above250 {
        condition month == APR .and. annMOK079FNF>=375.
        value 60. - 6. - 6.5}
    case Below250 {
        condition month == APR
        value 39. - 3. - 3.25}
    case otherwise {
        condition always
        value AnnAlloc60n_NA4DV(-1)}
}        
goal setAnnAlloc60n_NA4DV {AnnAlloc60N_NA4DV = AnnAlloc60n_NA4}

define AWRann_60N_NA4dv {std kind 'ANNUAL-APPLIED-WATER' units 'taf'}
define AWRann_60N_NA4{   
       case octBgnWY {  ! special case for first wateryear, Sum Oct-Sep
            condition month == OCT .and. wateryear == bgnWY
                sum(i=0,11,1)  AWR_60N_NA4(i)*cfs_taf(i)}
       case MarEndWY {  ! special case for last wateryear, Sum Oct-Sep
            condition month == MAR .and. wateryear == EndWY
                sum(i=prevOCT,SEP-month,1) AWR_60N_NA4(i)*cfs_taf(i)}
       case March { ! March for wateryears BgnWY - year before EndWY
            condition month == MAR .and. wateryear < EndWY
                sum(i=0,11,1)  AWR_60N_NA4(i)*cfs_taf(i)}
       case Rest { 
            condition always
                value AWRann_60N_NA4dv(-1) } }
goal setAWRann_60N_NA4dv {AWRann_60N_NA4dv = AWRann_60N_NA4}

define AWOann_60N_NA4dv {std kind 'ANNUAL-APPLIED-WATER' units 'taf'}
define AWOann_60N_NA4{   
       case octBgnWY {  ! special case for first wateryear, Sum Oct-Sep
            condition month == OCT .and. wateryear == bgnWY
                sum(i=0,11,1)  AWO_60N_NA4(i)*cfs_taf(i)}
       case MarEndWY {  ! special case for last wateryear, Sum Oct-Sep
            condition month == MAR .and. wateryear == EndWY
                sum(i=prevOCT,SEP-month,1) AWO_60N_NA4(i)*cfs_taf(i)}
       case March { ! March for wateryears BgnWY - year before EndWY
            condition month == MAR .and. wateryear < EndWY
                sum(i=0,11,1)  AWO_60N_NA4(i)*cfs_taf(i)}
       case Rest { 
            condition always
                value AWOann_60N_NA4dv(-1) } }
goal setAWOann_60N_NA4dv {AWOann_60N_NA4dv = AWOann_60N_NA4}

define dist_60N_NA4 {value (AWR_60N_NA4+AWO_60N_NA4)*cfs_taf/(AWRann_60N_NA4+AWOann_60N_NA4) }
define dist60N_NA4_ {alias dist_60N_NA4 kind 'debug' units 'none'}

goal setD_MOK035_60N_NA4 {D_MOK035_60N_NA4 < AnnAlloc60N_NA4 * dist_60N_NA4 * taf_cfs}


!******************************************************************************
! Woodbridge Provision to City of Stockton
!******************************************************************************

define AnnAlloc60S_NU1dv {std kind 'AnnualAlloc' units 'taf'}
define AnnAlloc60S_NU1 {
    case octBgnWY {  ! special case for first wateryear, Sum Oct-Sep
        condition month == OCT .and. wateryear == bgnWY
        value 6.5}
    case Above250 {
        condition month == APR .and. annMOK079FNF>=375.
        value 6.5}
    case Below250 {
        condition month == APR
        value 3.25}
    case otherwise {
        condition always
        value AnnAlloc60S_NU1dv(-1)}
}        
goal setAnnWIDtoCoSdv {AnnAlloc60S_NU1dv = AnnAlloc60S_NU1}
define dist_60S_NU1 {select Stockton from Mok_demands where month=month}
goal zeroD_MOK035_WTPDWS {D_MOK035_WTPDWS < AnnAlloc60S_NU1 * dist_60S_NU1 * taf_cfs}

!******************************************************************************
! Woodbridge Provision to City of Lodi
!******************************************************************************
/*
In May 2003, the City entered into an agreement with Woodbridge Irrigation District to purchase 6,000
acre-feet per year of surface water from the Mokelumne River (via WID canal facilities) for a period of
40 years. An amendment approved in January 2008 extended the agreement to 2047.

During dry years when WID’s supply is reduced, the City’s allotment can be reduced down to
3,000 AFY.

Diversion of the water from the Mokelumne River is permitted only from March 1 through
October 15. However, an agreement with East Bay Municipal Utilities District (EBMUD) will
allow release of up to 1,000 acre-feet between October 15 and the end of February. This will
allow year-round operation of the surface water treatment plant.
*/
define AnnAlloc60n_NU1DV {std kind 'AnnualAlloc' units 'taf'}
define AnnAlloc60n_NU1 {
    case octBgnWY {  ! special case for first wateryear, Sum Oct-Sep
        condition month == OCT .and. wateryear == bgnWY
        value 6.}
    case Above250 {
        condition month == APR .and. annMOK079FNF>=375.
        value 6.}
    case Below250 {
        condition month == APR
        value 3.}
    case otherwise {
        condition always
        value AnnAlloc60n_NU1DV(-1)}
}        
goal setAnnAlloc60n_NU1DV {AnnAlloc60n_NU1DV = AnnAlloc60n_NU1}

define distWIDto60N_NU1 {select Lodi from Mok_demands where month=month}
goal setD_MOK035_60N_NU1 {D_MOK035_60N_NU1 < AnnAlloc60n_NU1 * distWIDto60N_NU1 * taf_cfs}



!******************************************************************************
! North San Joaquin Water Conservation District (60N_NA3)
!******************************************************************************
/*
North San Joaquin Water Conservation District (NSJWCD) has a junior water right that
is derived from Permit 10478. It provides a permit for temporary appropriation of water that is excess to the
needs of a municipality. The temporary permit allows NSJWCD to divert from December 1 to July 1
up to 20,000 AFY from the Mokelumne River, either directly or to storage in EBMUD’s Camanche
Reservoir. NSJWCD has an agreement with EBMUD to store water in Camanche Reservoir, when
water is available under NSJWCD’s junior water right, for later release to NSJWCD. NSJWCD
historically has used as much as 9,488 AFY under Permit 10477, and in the last 10 years, diversions
have declined to between 2,000 and 4,000 AFY. In dry and critically dry years, when there is no
stored water surplus to meet EBMUD’s municipal needs, NSJWCD typically receives no water under
its entitlement. The maximum diversion rate is 80 cfs.
*/


! keep a running total of the volume available Dec-Jun
define Alloc60n_NA3DV {std kind 'AnnualAlloc' units 'taf'}
define Alloc60n_NA3 {
    case December {
        condition month==DEC
        value 20.}
    case JanThruJun {
        condition month>DEC .and. month<JUL
        value max(0., Alloc60n_NA3DV(-1) - D_MOK050_60N_NA3(-1)*cfs_taf(-1)) }
    case JulThruOct {
        condition always
        value 0.}
}        
goal setAlloc60n_NA3DV {Alloc60n_NA3DV = Alloc60n_NA3}

goal setD_MOK050_60N_NA3 {D_MOK050_60N_NA3 < min(80., Alloc60n_NA3 * taf_cfs)}

!******************************************************************************
!EBMUD
!******************************************************************************

/*
EBMUD introduces rationing/conservation measures when the forecasted total system storage
at the end of September is less than 500,000 acre-feet.

The supply storage projected in April for the end of a water year in the Water Supply and Availability Report
determines the need for requiring customers to change their water use. The DMP level of rationing is dependent on
supply storage, modeled after the DMP guidelines established in the 1992 Contingency Plan and modifi ed to
reflect the maximum 15 percent rationing goal as adopted by the Board in October 2009. The DMP follows this
process:
   - based on water year runoff predicted in April, estimate total system storage that will be available at end of
     water year (September 30);
   - if total system storage is projected to be less than 500 TAF, prepare and implement a DMP; and adjust the DMP
     as conditions change during an extended dry period.

From the 2010 UWMP:
   Forecasted System Storage > 500 TAF:   No rationing
   Forecasted System Storage 450-500 TAF: 0%-10% rationing goal
   Forecasted System Storage 300-450 TAF: 10%-15% rationing goal
   Forecasted System Storage < 300 TAF:   15% goal
   
In the first year of the frought decrease rationing by 50% as a surrogate for delays in EBMUD planning process and
reaction time of consumers to save water.

*/

!define UD_EBMUD {timeseries kind 'URBAN-DEMAND' units 'TAF' convert 'CFS'}  !already defined in eastsidestreams\arcs-deliveries.wresl

! Inflows minus demands from Mar-Sep
define EBMUD_WS1 {
	case AprtoSep	{
		condition month == APR
	  	sum(i=0,5) MOK079FNF(i) + I_PARDE(i)*cfs_taf(i) + I_CMCHE(i)*cfs_taf(i)- UD_EBMUD(i)*cfs_taf(i)}
	case other {
	  	condition always
	  	value 0.0}
}
define EBMUD_WS2 {
	case AprtoSep	{
		condition month == APR
	  	value
	  	  AnnualAMADR * 0.600     !Upstream demand Apr-Sep
	  	+ AnnualCPUD  * 0.600     !Upstream demand Apr-Sep
	  	+ AnnualCCWD  * 0.600     !Upstream demand Apr-Sep
                + 0.                      !North San Joaquin WCD rights are junior to EBMUD
                + AnnAlloc60N_NA4 * 0.970 !Woodbridge ID demand Apr-Sep based on average of 1922-2009
                + AnnAlloc60N_NU1 * 0.546 !City of Lodi demand Apr-Sep
                + AnnAlloc60N_NU1 * 0.847 !City of Stockton demand Apr-Sep
                + AnnAlloc60N_NA5 * 0.960 !Riparian Diversions Apr-Sep based on average of 1922-2009
                + 0.70*56*6/12 }          !Groundwater seepage. EBMUD assumes 70% of the losses occur btw Camanche and Woodbridge Dam. In dry years losses are assumed to be 56 TAF/year.
	case other {
	  condition always
	  value	0.0}
}
define EBMUD_WS3 {
	case MartoSep	{
		condition month == APR
    		select WBAprSepTAF from Mok_JSAFlow where month=month, wyt=wyt_MokJSA}
	case other {
		condition always
	  	value 0.0}
}

! Calculate the supply index for Mokelumne River for end of September
! Sum of End of Mar storage and available inflows from Apr-Sep
define EBMUD_SIdv {std kind 'SUPPLY-INDEX' units 'TAF'}

define EBMUD_SI   {
	case Initialize {
		condition month == BgnMo .and. wateryear == BgnWY
		value 1000.}
	case April {
		condition month == APR
		value max(0, S_PARDE(-1) + S_CMCHE(-1) + S_EBTML(-1) + EBMUD_WS1 - EBMUD_WS2 - EBMUD_WS3) }
	case other {
		condition always
	  	value EBMUD_SIdv(-1)	}
}

goal set_EBMUD_SIdv {EBMUD_SIdv = EBMUD_SI }

! If the supply index compted above is below 500 TAF, then the current year is assumed to be a water-deficit year.
! The flag number of '1' indicates water-deficit years.
define EBMUD_DefFlagdv {std kind 'FLAG' units 'NONE'}

define EBMUD_DefFlag  {
	case Initialize {
	        condition month == BgnMo .and. wateryear == BgnWY
		value 0.}
	case ContinuingDefic {
    		condition month == APR .and. EBMUD_SI <= 500. .and. EBMUD_SIdv(-1) <= 500.
    		value 1.0  }
    	case Defic {
	    	condition month == APR .and. EBMUD_SI <= 500. .and. EBMUD_SIdv(-1) > 500.
    		value 0.5  }
	case NoDefic {
    		condition month == APR
    		value 0.0  }
     	case notmarch {
     	 	condition always
     	 	value EBMUD_DefFlagdv(-1) }
}

goal set_EBMUD_DefFlagdv {EBMUD_DefFlagdv = EBMUD_DefFlag }

! determine the cut in deliveries to the Mokelumne Aqueduct
define EBMUD_PerCut {select percut_mi from Mok_percut given SI=EBMUD_SI use linear }
define EBMUD_PerDel {value 1. - EBMUD_PerCut*EBMUD_DefFlag}

goal maxD90_PAG {D_PARDE_MAQ001 < EBMUD_PerDel * UD_EBMUD}

define EBMUD_WS1dv    {alias EBMUD_WS1 kind 'Index' units 'TAF'}
define EBMUD_WS2dv    {alias EBMUD_WS2 kind 'Index' units 'TAF'}
define EBMUD_WS3dv    {alias EBMUD_WS3 kind 'Index' units 'TAF'}
define EBMUD_PerCutdv {alias EBMUD_PerCut kind 'Rationing' units 'None'}
define EBMUD_PerDeldv {alias EBMUD_PerDel kind 'Allocation' units 'None'}
define EBMUD_TotalStoragedv {alias S_PARDE + S_CMCHE + S_EBTML kind 'Storage' units 'TAF'}